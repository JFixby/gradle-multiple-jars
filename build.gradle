apply plugin: "java"
apply plugin: "maven"

group = "com.github.jitpack"
version = 1.0
sourceCompatibility = 1.8 // java 8
targetCompatibility = 1.8
	
repositories {
    mavenCentral()
}

sourceSets {
    api
    impl
}

sourceSets.all { set ->
    def jarTask = task("${set.name}Jar", type: Jar) {
        baseName = "$set.name"
        from set.output
    }

    artifacts {
        archives jarTask
    }
}

dependencies {
    apiCompile 'commons-codec:commons-codec:1.5'

    implCompile sourceSets.api.output
    implCompile 'commons-lang:commons-lang:2.6'

    testCompile 'junit:junit:4.9'
    testCompile sourceSets.api.output
    testCompile sourceSets.impl.output
    
    runtime configurations.apiRuntime
    runtime configurations.implRuntime
}

jar {
    from sourceSets.api.output
    from sourceSets.impl.output
}

// this step is necessary when installing both jars in the local maven repository
install {
    repositories.mavenInstaller {
		addFilter('api') { artifact, file -> artifact.name.endsWith('api') }
		addFilter('impl') { artifact, file -> artifact.name.endsWith('impl') }
    }
}
